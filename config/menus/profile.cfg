profilebuttons = [
    guilist [ guifont "emphasis" [
        guistrut 2
        guibutton "^fgok" [
            setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevloadweap $playerprevrandweap
            cleargui 1
        ] []
        guistrut 3
        if (needname) [
            guibutton "^fomain menu" [showgui main]
        ] [
            guibutton "^focancel" [cleargui 1]
        ]
    ] ]
]

newgui profilediff [
    guiheader "profile"
    guitext "Do you want to save"
    guitext "the following changes?"
    guistrut 1
    looplist i [name colour model vanity loadweap randweap] [
        if (!=s $[player@i] $[playerprev@i]) [
            guicenter [
                guitext @i
            ]
            skip = 0
	]
    ]
    if $skip [cleargui 1]
    guistrut 1
    profilebuttons
] [
    skip = 1
]


profilepreview = [
    guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.6 1 1
]

profiletab = [guitwo [profilepreview] [@arg1] [profilebuttons] ] // left, right, bottom right

playerprevinherit = 0
playerprevdisinherit = 0

newgui playerprev [
    guiheader "player preview"
    guicenter [ guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [cleargui 1] 15 1 1 ]
    guistrut 0.25
    guicenter [
        guitext "team: "
        teamneutraltex = $teamtex
        loop t 5 [
            push t (at "neutral alpha omega kappa sigma" $t) [
                guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [playerprevteam = @@@@t] ] ]
            ]
        ]
        guistrut 1
        guitext "weapon: "
        loop w (- $weapidxnum 1) [ // exclude last id: melee
            push w (at $weapname $w) [
                guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [playerprevweap = @@@@w] ] ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevinherit [ playerprevinherit = 0; playerprevdisinherit = 1 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
    ]
]

loadweapall = 0
loadweaps = [
    lwa = ""
    lwl = (listlen $playerprevloadweap)
    lwo = (? (> $lwl $arg1) (at $playerprevloadweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $playerprevloadweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [ lw3 = $lwo ]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ playerprevloadweap = $lwa ]
    playerprevweap = $arg2
]

randweaps = [
    rwa = ""
    rwl = (listlen $playerprevrandweap)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $playerprevrandweap $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ playerprevrandweap = $rwa ]
]

// these names should match the types in config/vanities.cfg
vantypename = [chest eyes scalp forehead ears face back [right foot] [left foot] [right shoulder] [left shoulder]]
vantypesel = -1
vansel = -1

newgui profile [
    if (needname) [
        guistrut 0.5
        guiheader welcome
        guicenter [guifont huge [guitext "Welcome to Red Eclipse" $emblemtex]]
        guistrut 0.5
        guicenter [guitext "To get ready for action, please set up your player profile:"] 
        guicenter [guitext "Use the other tabs of this menu to choose a name, a profile colour and a pair of loadout weapons."]
        guistrut 0.5
	guicenter [guitext "To learn more about the weapons and many other aspects of the game, you can consult the help menus."]
        guicenter [guitext (format "You can press %1 any time to open these menus, and %2 to return to the previous menu." (dobindsearch [showgui help]) (dobindsearch [if (cleargui 1) [] [showgui main]]))]
	guicenter [guitext "During a match, this allows you to quickly look up rules and tips for the current game mode."]
        guicenter [guitext "Most of this information is also available on the Red Eclipse wiki, ^fchttp://redeclipse.net/wiki/"]
        guistrut 0.5
        guicenter [guitext "Once you have set up your profile with the ^fgok^fw button, you can start playing offline or online."]
        guicenter [guitext "Learning by doing or watching experienced players, both are great ways to get you started."]
        guicenter [guitext (format "You can press %1 to enter spectator mode and %2 to toggle first or third person view." (dobindsearch [spectate 1]) (dobindsearch thirdpersonswitch))]
        guicenter [guitext "Use the mouse wheel to follow the view of different players while spectating."]
        guistrut 0.5
        guicenter [guitext (format "One last tip: You can perform wall-runs and kicks with the %1 key. Have fun!" (dobindsearch special))]
        guitab profile
    ] [guiheader profile] 
    profiletab [
        guilist [
            guistrut 3
            guilist [
                guicenter [ guifont "emphasis" [ guitext "player name:" ] ]
                guicenter [ guifield playerprevname 24 [] -1 0 "" 0 "^fzad<enter name>" ]
                guistrut 1
                guicenter [ guifont "emphasis" [ guitext "player color:" ] ]
                guicenter [
                    if $playerprevcolour [guihexpreview $playerprevcolour "hex-value"] [
                        w = (at $weapname $playerprevweap)
                        guistayopen [ guiimage $[@[w]tex] [
                            playerprevweap = (+ 1 $playerprevweap)
                            if (= $playerprevweap (- $weapidxnum 1)) [playerprevweap = 0 ]
                        ] 1.9 1 "" [] $[@[w]colour] ]
                        guistrut 4.45
                    ]
                    guistrut 1
                    guilist [
                        guirgbsliders playerprevcolour
                    ]
                ]
                guistrut 0.2
                guiright [guiradio "use the current weapon's colour  " playerprevcolour 0]
                guistrut 0.5
                guicenter [ guifont "emphasis" [ guitext "player model:" ] ]
                guistrut 0.25
                guicenter [
                    guiradio "male" playerprevmodel 0
                    guistrut 1.5
                    guiradio "female" playerprevmodel 1
                ]
            ]
        ]
    ]
    guitab "loadout"
    profiletab [
        guilist [
            guistrut 3
            guilist [
                guistrut 0.75
                guifont "emphasis" [
                    guicenter [ guitext "CHOOSE YOUR WEAPONS" ]
                ]
                guistrut 0.5
                guifont "little" [
                    if (= $getplayerstate 0) [
                        guicenter [ guitext "^fs^frWeapon changes will not take^fS"]
                        guicenter [ guitext "^fs^freffect until after respawn!"]
                    ] [
                        guicenter [ guitext "you will spawn with these weapons" ]
                        guicenter [ guitext (format "you can carry ^fs^fc%1^fS %2" $playermaxcarry (? (= $playermaxcarry 1) "weapon" "weapons")) ]
                    ]
                ]
                guistrut 0.5
                guilist [
                    guistrut 3
                    guilist [
                        guicenter [ guitext "random weapon filter:" ]
                        guistayopen [
                            gdr = (listlen $playerprevrandweap)
                            guilist [
                                guibackground 0x303030
                                guispring 1
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    w3 = (? (> $gdr $w1) (at $playerprevrandweap $w1) 1)
                                    guilist [
                                        if $w3 [ guibackground 0xFFFFFF 0.01 $[@[w5]colour] 1 1 ]
                                        guistrut 0.125
                                        guiimage textures/weapons/@w5 [
                                            rwv = (? (> (listlen $playerprevrandweap) @w1) (at $playerprevrandweap @w1) 1)
                                            randweaps @w1 (! $rwv)
                                        ] 0.75 0 [textures/blank] [] (? $w3 $[@[w5]colour] 0x808080)
                                        guistrut 0.125
                                    ]
                                ]
                            ]
                        ]
                        guistrut 1
                        guicenter [ guicheckbox " show all weapon slots" loadweapall ]
                        guifont "little" [
                            guistrut 0.5
                            guicenter [ guitext "(if a weapon is not available, the" ]
                            guicenter [ guitext "next one in the list will be used)" ]
                            guistrut 0.5
                        ]
                    ]
                    guistrut 3
                ]
                guistrut 0.5
                guicenter [ guicheckbox " pick a loadout every new game" showloadoutmenu ]
            ]
            guistrut 2
            guilist [
                guibackground
                guicenter [
                    guistayopen [
                        gdl = (listlen $playerprevloadweap)
                        gdw = 0
                        loop w2 (? $loadweapall $weapidxloadout $playermaxcarry) [
                            w3 = (? (> $gdl $w2) (at $playerprevloadweap $w2) -1)
                            hi = (mod $w2 2)
                            al = (|| (= $w3 0) (allowedweap $w3))
                            guilist [
                                guibackground (? (&& $al (< $gdw $playermaxcarry)) 0x303030 0x000000)
                                guistrut 0.25
                                guicenter [ guitext (format "%1%2" (? $hi "^fd" "^fw") (+ $w2 1)) ]
                                guistrut 0.5
                                guilist [
                                    if (= $w3 0) [ guibackground 0xFFFFFF 0.01 0xFFFFFF 1 1 ]
                                    guistrut 0.125
                                    guiimage [textures/question] [loadweaps @w2 0] 0.75 0 [textures/blank] [] (? (= $w3 0) 0xFFFFFF 0x808080)
                                    guistrut 0.125
                                ]
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    guilist [
                                        if (= $w3 $w4) [ guibackground 0xFFFFFF 0.01 $[@[w5]colour] 1 1 ]
                                        guistrut 0.125
                                        guiimage (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) [loadweaps @w2 @w4] 0.75 0 [textures/blank] [] (? (= $w3 $w4) $[@[w5]colour] 0x808080)
                                        guistrut 0.125
                                    ]
                                ]
                            ]
                            if $al [ gdw = (+ $gdw 1) ]
                        ]
                        if (! $loadweapall) [
                            guistrut 1.8
                            guilist [
                                guistrut 1.5
                                guispring 1
                                guicenter [guibutton "random" [loadweaps 0 0] [loadweaps 1 0]]
                                guispring 1
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    guibutton (format "%1" $[@[w5]longname]) [loadweaps 0 (+ $weapidxoffset @w1)] [loadweaps 1 (+ $weapidxoffset @w1)] "" $[@[w5]colour]
                                    cases $guirollovername "random" [
                                        guitooltip "You will get a random weapon each time you spawn" 1500
                                    ] (format "%1" $[@[w5]longname]) [
                                      guitooltip (format "^f[%1]primary mode:^n^fw%2^n^f[%1]secondary mode:^n^fw%3" $[@[w5]colour] $[@[w5]desc1] $[@[w5]desc2]) 1500
                                    ]
                                    guispring 1
                                ]
                            ]
                        guistrut 1.8
                        ]
                    ]
                ]
            ]
        ]
    guivisible [ guitip (format "press %1 to open this weapon menu at any time" (dobindsearch "showgui profile 2")) ]
    ] 
    guitab "vanity items"
    profiletab [
        guilist [
            guistrut 3
            guilist [
                guifont "emphasis" [
                    guicenter [ guitext "CHOOSE YOUR VANITIES" ]
                ]
                guistrut 0.5
                guifont "little" [
                    guicenter [ guitext "these decorative items do not influence movement or abilities in any way" ]
                    guistrut 0.5
                    guistrut 70 1
                    guilist [
                        guilist [
                            guistrut 32 1
                            guitext "select a body part:"
                            guistrut 0.5
                            loop i (listlen $vantypename) [
                                t = (format "^fd%1: nothing" (at $vantypename $i))
                                loop j (getvanity) [
                                    if (= (getvanity $j 0) $i) [
                                        if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [
                                            t = (format "%1:^fy %2" (at $vantypename $i) (getvanity $j 2))
                                        ]
                                    ]
                                ]
                                guiradio $t vantypesel $i [
                                    vansel = -1
                                    loop j (getvanity) [
                                        if (= (getvanity $j 0) @i) [
                                            if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [vansel = $j]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                        guistrut 2
                        guilist [
                            if (> $vantypesel -1) [
                                guitext (format "options for the %1:" (at $vantypename $vantypesel))
                                guistrut 0.5
                                guiradio "nothing" vansel -1 [
                                    loop j (getvanity) [
                                        if (= (getvanity $j 0) $vantypesel) [
                                            playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                                        ]
                                    ]
                                ]
                            ]
                            loop i (getvanity) [
                                if (= (getvanity $i 0) $vantypesel) [
                                    guiradio (getvanity $i 2) vansel $i [
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $vantypesel) [
                                                playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                                            ]
                                        ]
                                        append playerprevvanity (getvanity @i 1)
                                    ]
                                ]
                            ]
                        ]
                        //guispring
                        //guilist [
                        //    guimodelpreview (getvanity $vansel 6) "" [] 5 1
                        //]
                    ]
                ]
            ]
        ]
    ]
    guitab "identity management"
        profiletab [
            guilist [
                guistrut 3
                guilist [
                    guifont "emphasis" [
                        guicenter [ guitext "Create your identity certificate" ]
                    ]
                    guistrut 0.5
                    guifont "little" [
                        guicenter [ guitext "Pick a public/private keypair" ]
                        guistrut 0.5
                        guistrut 70 1
                        guilist [


                        ]
                    ]

                   loop i (getuserident) [
                       guiradio (getuserident $i 0) curuserident $i
                    ]
                ]
            ]
        ]
] [
    if (= $guipasses 0) [
        if $playerprevdisinherit [ playerprevdisinherit = 0 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
        playerprevloadweap = ""
        break = 0
        loopwhile i $weapidxloadout [= $break 0] [
            q = (getloadweap $i)
            if (< $q 0) [ break = 1 ] [ playerprevloadweap = (? $i (concat $playerprevloadweap $q) $q) ]
        ]
        playerprevrandweap = ""
        break = 0
        loopwhile i $weapidxloadout [= $break 0] [
            q = (getrandweap $i)
            playerprevrandweap = (? $i (concat $playerprevrandweap $q) $q)
        ]
    ]
]

newgui useraccount [
    guiheader "user account"
    guilist [
        guistrut 2
        guilist [
            guistrut 0.5
            guifont "emphasis" [ guicenter [ guitext "RED ECLIPSE USER ACCOUNT" ] ]
            guistrut 0.5
            guicenter [ guitext "unique user accounts provide you with an established identity in the" ]
            guicenter [ guitext "community, and allow server owners to add you as a local moderator." ]
            guicenter [
                guitext "the icon before your name will also change to player status: "
                guinohitfx [ guiimage "textures/privs/player" [] 0.5 ]
            ]
            guistrut 0.5
            guicenter [ guitext "enter the account login information received in your email below:" ]
            guistrut 0.5
            guicenter [ guitext "player handle:" ]
            guicenter [ guifield accountname 48 [] ]
            guistrut 0.5
            guicenter [ guitext "private key:" ]
            guicenter [ guifield accountpass 48 [] ]
            guistrut 1
            guicenter [ guicheckbox "use my account online" authconnect ]
            guicenter [ guicheckbox "use my account offline" quickauthchecks ]
            guistrut 1
            guicenter [ guitext "new account applications and replacement accounts are available at:" ]
            guicenter [ guitext "^fghttp://redeclipse.net/apply" ]
            guistrut 1
        ]
        guistrut 2
    ]
]
